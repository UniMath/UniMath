(******************************************************************************************

 The truth formula of partial setoids

 In this file, we construct the truth formula in the category of partial setoids. Here we
 use the characterization of subobjects in terms of formulas as given in the file
 `SubobjectDispCat.v`.

 The construction of the connectives of subobjects of partial setoids is similar to how
 connectives are defined for subsets. For the truth formula, we take the whole set, so all
 elements that are defined (i.e. reflexive according to the partial equivalence relation).

 Content
 1. The formula
 2. Introduction rule
 3. Stability under substitution
 4. The fiberwise terminal object

 ******************************************************************************************)
Require Import UniMath.MoreFoundations.All.
Require Import UniMath.CategoryTheory.Core.Prelude.
Require Import UniMath.CategoryTheory.DisplayedCats.Core.
Require Import UniMath.CategoryTheory.DisplayedCats.Fibrations.
Require Import UniMath.CategoryTheory.DisplayedCats.Fiber.
Require Import UniMath.CategoryTheory.DisplayedCats.Fiberwise.FiberwiseTerminal.
Require Import UniMath.CategoryTheory.Hyperdoctrines.Hyperdoctrine.
Require Import UniMath.CategoryTheory.Hyperdoctrines.FirstOrderHyperdoctrine.
Require Import UniMath.CategoryTheory.Hyperdoctrines.PartialEqRels.PERs.
Require Import UniMath.CategoryTheory.Hyperdoctrines.PartialEqRels.PERMorphisms.
Require Import UniMath.CategoryTheory.Hyperdoctrines.PartialEqRels.PERCategory.
Require Import UniMath.CategoryTheory.Hyperdoctrines.PartialEqRels.Logic.SubobjectDispCat.

Local Open Scope cat.
Local Open Scope hd.

Section TruthFormula.
  Context (H : first_order_hyperdoctrine).

  (** * 1. The formula *)
  Proposition per_subobject_truth_laws
              (Î“ : partial_setoid H)
    : per_subobject_laws (tm_var Î“ ~ tm_var Î“).
  Proof.
    split.
    - use forall_intro.
      use impl_intro.
      use weaken_right.
      hypersimplify.
      apply hyperdoctrine_hyp.
    - do 2 use forall_intro.
      use impl_intro.
      use weaken_right.
      use impl_intro.
      hypersimplify.
      pose (Î³â‚ := Ï€â‚‚ (Ï€â‚ (tm_var ((ğŸ™ Ã—h Î“) Ã—h Î“)))).
      pose (Î³â‚‚ := Ï€â‚‚ (tm_var ((ğŸ™ Ã—h Î“) Ã—h Î“))).
      fold Î³â‚ Î³â‚‚.
      use partial_setoid_refl_r.
      {
        exact Î³â‚.
      }
      use weaken_left.
      apply hyperdoctrine_hyp.
  Qed.

  Definition per_subobject_truth
             (Î“ : partial_setoid H)
    : per_subobject Î“.
  Proof.
    use make_per_subobject.
    - exact (tm_var _ ~ tm_var _).
    - exact (per_subobject_truth_laws Î“).
  Defined.

  (** * 2. Introduction rule *)
  Proposition per_subobject_truth_intro
              {Î“ : partial_setoid H}
              (Ï† : per_subobject Î“)
    : per_subobject_mor_law
        (id_partial_setoid_morphism Î“)
        Ï†
        (per_subobject_truth Î“).
  Proof.
    do 2 use forall_intro.
    use impl_intro.
    use weaken_right.
    use impl_intro.
    cbn.
    hypersimplify.
    pose (Î³â‚ := Ï€â‚‚ (Ï€â‚ (tm_var ((ğŸ™ Ã—h Î“) Ã—h Î“)))).
    pose (Î³â‚‚ := Ï€â‚‚ (tm_var ((ğŸ™ Ã—h Î“) Ã—h Î“))).
    fold Î³â‚ Î³â‚‚.
    use partial_setoid_refl_r.
    {
      exact Î³â‚.
    }
    use weaken_left.
    apply hyperdoctrine_hyp.
  Qed.

  (** * 3. Stability under substitution *)
  Proposition per_subobject_truth_subst
              {Î“â‚ Î“â‚‚ : partial_setoid H}
              (s : partial_setoid_morphism Î“â‚ Î“â‚‚)
    : per_subobject_mor_law
        (id_partial_setoid_morphism Î“â‚)
        (per_subobject_truth Î“â‚)
        (per_subobject_subst s (per_subobject_truth Î“â‚‚)).
  Proof.
    do 2 use forall_intro.
    use impl_intro.
    use weaken_right.
    use impl_intro.
    cbn.
    hypersimplify.
    pose (Î³â‚ := Ï€â‚‚ (Ï€â‚ (tm_var ((ğŸ™ Ã—h Î“â‚) Ã—h Î“â‚)))).
    pose (Î³â‚‚ := Ï€â‚‚ (tm_var ((ğŸ™ Ã—h Î“â‚) Ã—h Î“â‚))).
    fold Î³â‚ Î³â‚‚.
    simple refine (exists_elim (partial_setoid_mor_hom_exists s _) _).
    - exact Î³â‚‚.
    - use weaken_left.
      use partial_setoid_refl_r.
      {
        exact Î³â‚.
      }
      apply hyperdoctrine_hyp.
    - unfold Î³â‚, Î³â‚‚ ; clear Î³â‚ Î³â‚‚.
      pose (Î³â‚ := Ï€â‚‚ (Ï€â‚ (Ï€â‚ (tm_var (((ğŸ™ Ã—h Î“â‚) Ã—h Î“â‚) Ã—h Î“â‚‚))))).
      pose (Î³â‚‚ := Ï€â‚‚ (Ï€â‚ (tm_var (((ğŸ™ Ã—h Î“â‚) Ã—h Î“â‚) Ã—h Î“â‚‚)))).
      pose (Î³â‚ƒ := Ï€â‚‚ (tm_var (((ğŸ™ Ã—h Î“â‚) Ã—h Î“â‚) Ã—h Î“â‚‚))).
      rewrite exists_subst.
      use exists_intro.
      {
        exact Î³â‚ƒ.
      }
      hypersimplify_form.
      hypersimplify.
      fold Î³â‚ Î³â‚‚ Î³â‚ƒ.
      use conj_intro.
      + use weaken_right.
        apply hyperdoctrine_hyp.
      + use weaken_right.
        use (partial_setoid_mor_cod_defined s).
        * exact Î³â‚‚.
        * apply hyperdoctrine_hyp.
  Qed.

  (** * 4. The fiberwise terminal object *)
  Definition fiberwise_terminal_per_subobject
    : fiberwise_terminal (disp_cat_per_subobject_cleaving H).
  Proof.
    use make_fiberwise_terminal_locally_propositional.
    - apply locally_prop_disp_cat_per_subobject.
    - exact per_subobject_truth.
    - intros Î“ Ï†.
      exact (per_subobject_truth_intro Ï†).
    - intros Î“â‚ Î“â‚‚ s.
      exact (per_subobject_truth_subst s).
  Defined.
End TruthFormula.
