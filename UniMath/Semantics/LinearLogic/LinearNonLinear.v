(****************************************************************************

 Linear-non-linear models of linear logic

 In this file, we define the notion of linear-non-linear models, which is
 one of the basic notions of models for linear logic. See for example
 - "Categorical Semantics of Linear Logic"
 - "A Mixed Linear Non-Linear Logic: Proofs, Terms, and Models"
 - "Categorical Models of Linear Logic Revisited"
 We also give a symmetric monoidal comonad arising from these models, which
 gives an interpretation to the !-modality.

 Contents
 1. Linear-non-linear models
 2. Accessors and builders
 3. !-modality

 ****************************************************************************)
Require Import UniMath.MoreFoundations.All.
Require Import UniMath.CategoryTheory.Core.Prelude.
Require Import UniMath.CategoryTheory.Adjunctions.Core.
Require Import UniMath.CategoryTheory.whiskering.
Require Import UniMath.CategoryTheory.Monads.Comonads.
Require Import UniMath.CategoryTheory.Monoidal.Categories.
Require Import UniMath.CategoryTheory.Monoidal.Functors.
Require Import UniMath.CategoryTheory.Monoidal.Adjunctions.
Require Import UniMath.CategoryTheory.Monoidal.FunctorCategories.
Require Import UniMath.CategoryTheory.Monoidal.Structure.Cartesian.
Require Import UniMath.CategoryTheory.Monoidal.Structure.Symmetric.
Require Import UniMath.CategoryTheory.Monoidal.Structure.Closed.

Import MonoidalNotations.

Local Open Scope cat.

(**
 1. Linear-non-linear models
 *)
Definition linear_non_linear_model
  : UU
  := âˆ‘ (ğ•ƒ : sym_mon_closed_cat) (* \bL *)
       (ğ•„ : sym_monoidal_cat) (* \bM *)
       (A : adjunction ğ•„ ğ•ƒ),
     is_cartesian ğ•„
     Ã—
     sym_monoidal_adjunction ğ•„ ğ•ƒ A.

(**
 2. Accessors and builders
 *)
#[reversible=no] Coercion linear_non_linear_model_to_linear
         (ğ•ƒ : linear_non_linear_model)
  : sym_mon_closed_cat
  := pr1 ğ•ƒ.

Definition cartesian_cat_from_linear_non_linear_model
           (ğ•ƒ : linear_non_linear_model)
  : sym_monoidal_cat
  := pr12 ğ•ƒ.

Definition adjunction_from_linear_non_linear_model
           (ğ•ƒ : linear_non_linear_model)
  : adjunction
      (cartesian_cat_from_linear_non_linear_model ğ•ƒ)
      ğ•ƒ
  := pr122 ğ•ƒ.

Proposition is_cartesian_cat_from_linear_non_linear_model
            (ğ•ƒ : linear_non_linear_model)
  : is_cartesian (cartesian_cat_from_linear_non_linear_model ğ•ƒ).
Proof.
  exact (pr1 (pr222 ğ•ƒ)).
Qed.

Definition sym_monoidal_adjunction_from_linear_non_linear_model
           (ğ•ƒ : linear_non_linear_model)
  : sym_monoidal_adjunction
      (cartesian_cat_from_linear_non_linear_model ğ•ƒ)
      ğ•ƒ
      (adjunction_from_linear_non_linear_model ğ•ƒ)
  := pr2 (pr222 ğ•ƒ).

Definition make_linear_non_linear
           (ğ•ƒ : sym_mon_closed_cat)
           (ğ•„ : sym_monoidal_cat)
           (A : adjunction ğ•„ ğ•ƒ)
           (HM : is_cartesian ğ•„)
           (HA : sym_monoidal_adjunction ğ•„ ğ•ƒ A)
  : linear_non_linear_model
  := ğ•ƒ ,, ğ•„ ,, A ,, HM ,, HA.

Definition make_linear_non_linear_from_strong
           (ğ•ƒ : sym_mon_closed_cat)
           (ğ•„ : sym_monoidal_cat)
           (A : adjunction ğ•„ ğ•ƒ)
           (HM : is_cartesian ğ•„)
           (HLâ‚ : fmonoidal ğ•„ ğ•ƒ (left_adjoint A))
           (HLâ‚‚ : is_symmetric_monoidal_functor ğ•„ ğ•ƒ HLâ‚)
  : linear_non_linear_model.
Proof.
  use make_linear_non_linear.
  - exact ğ•ƒ.
  - exact ğ•„.
  - exact A.
  - exact HM.
  - use sym_monoidal_adjunction_from_strong.
    + exact HLâ‚.
    + exact HLâ‚‚.
Defined.

(**
 3. !-modality
 *)
Definition bang_modality
           (ğ•ƒ : linear_non_linear_model)
  : sym_monoidal_cmd ğ•ƒ
  := sym_monoidal_adjunction_to_sym_monoidal_cmd
       (adjunction_from_linear_non_linear_model ğ•ƒ)
       (sym_monoidal_adjunction_from_linear_non_linear_model ğ•ƒ).
