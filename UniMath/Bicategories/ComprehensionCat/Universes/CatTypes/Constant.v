(**

 Codes for types in the empty context (for categories)

 Our goal is to express that some universe in a category is closed under type formers,
 and in this file we consider the following: unit types, empty types, natural numbers,
 and subobject classifiers. Specifically, we define the following:
 - when a universe contains codes for these types
 - when a functor preserves those codes
 We also develop the main infrastructure necessary to construct univalent displayed
 bicategories representing that the universe is closed under the aforementioned type
 formers.

 There are several ideas in the development. The first important observation is that
 the aforementioned types (unit, empty, natural numbers, subobject classifier) are
 essentially determined by a type in the empty context. From the point of view of
 type theory, we can see it as follows: we have a type `ùüô` (unit type) in the empty
 context, and the unit type in any other context is obtained by weakening. For this
 reason, it suffices to add a code for the desired type only in the empty context
 meaning that we do not need to add coherence conditions.

 To express that a universe contains some type, we use another idea. Specifically,
 we say that there is a code (i.e., morphism `ùüô --> u` from the terminal object to
 the universe type) whose associated type is isomorphic to the given type. This
 means that we assume that the category has enough structure (e.g., we assume that
 the category has an initial objects to express that the universe has codes for
 the empty type).

 Finally, we give a uniform treatment of all these type formers. Specifically, we
 give a general definition that says when a universe contains a code for a given
 type. Note that this treatment is only for a fixed type in the empty context,
 because for other type formers (like ‚àè-types) one would need coherence conditions.

 Contents
 1. Codes for a type
 2. Examples for codes
 2.1. The terminal object
 2.2. The initial object
 2.3. Parameterized natural numbers object
 2.4. Subobject classifier
 3. Preservation by functors
 4. Examples for preservation
 4.1. Preservation of the code for the terminal object
 4.2. Preservation of the code for the initial object
 4.3. Preservation of the code for the parameterized natural numbers object
 4.4. Preservation of the code for the subobject classifier
 5. Preservation by identity and composition
 6. Instantiation to the examples
 6.1. Preservation of the code for the terminal object
 6.2. Preservation of the code for the initial object
 6.3. Preservation of the code for the parameterized natural numbers object
 6.4. Preservation of the code for the subobject classifier
 7. Univalence condition
 7.1. Univalence condition for the terminal object
 7.2. Univalence condition for the initial object
 7.3. Univalence condition for the parameterized natural numbers object
 7.4. Univalence condition for the subobject classifier

                                                                                           *)
Require Import UniMath.MoreFoundations.All.
Require Import UniMath.CategoryTheory.Core.Prelude.
Require Import UniMath.CategoryTheory.Limits.Initial.
Require Import UniMath.CategoryTheory.Limits.Terminal.
Require Import UniMath.CategoryTheory.Limits.BinProducts.
Require Import UniMath.CategoryTheory.Limits.Pullbacks.
Require Import UniMath.CategoryTheory.Limits.Preservation.
Require Import UniMath.CategoryTheory.Monics.
Require Import UniMath.CategoryTheory.SubobjectClassifier.SubobjectClassifier.
Require Import UniMath.CategoryTheory.SubobjectClassifier.PreservesSubobjectClassifier.
Require Import UniMath.CategoryTheory.SubobjectClassifier.SubobjectClassifierIso.
Require Import UniMath.CategoryTheory.Arithmetic.ParameterizedNNO.
Require Import UniMath.Bicategories.Core.Bicat.
Import Bicat.Notations.
Require Import UniMath.Bicategories.Core.Examples.StructuredCategories.
Require Import UniMath.Bicategories.ComprehensionCat.Universes.CatWithUniv.CatWithOb.
Require Import UniMath.Bicategories.ComprehensionCat.Universes.CatWithUniv.UniverseInCat.
Require Import UniMath.Bicategories.ComprehensionCat.Universes.CatWithUniv.UniverseDispBicat.

Local Open Scope cat.

Section TypesInCatUniv.
  Context {C : univ_cat_with_finlim_universe}.

  Let el : cat_stable_el_map_coherent C := univ_cat_cat_stable_el_map C.

  Local Notation "ùüô" := (terminal_univ_cat_with_finlim C).

  (** * 1. Codes for a type *)
  Definition type_in_cat_univ
             (A : C)
    : UU
    := ‚àë (a : ùüô --> univ_cat_universe C),
       z_iso (cat_el_map_el el a) A.

  Proposition isaset_type_in_cat_univ
              (A : C)
    : isaset (type_in_cat_univ A).
  Proof.
    use isaset_total2.
    - apply homset_property.
    - intro.
      apply isaset_z_iso.
  Qed.

  Definition make_type_in_cat_univ
             {A : C}
             (a : ùüô --> univ_cat_universe C)
             (f : z_iso (cat_el_map_el el a) A)
    : type_in_cat_univ A
    := a ,, f.

  Definition type_in_cat_univ_code
             {A : C}
             (a : type_in_cat_univ A)
    : ùüô --> univ_cat_universe C
    := pr1 a.

  Definition type_in_cat_univ_z_iso
             {A : C}
             (a : type_in_cat_univ A)
    : z_iso (cat_el_map_el el (type_in_cat_univ_code a)) A
    := pr2 a.

  Proposition type_in_cat_univ_eq
              {A : C}
              {a‚ÇÅ a‚ÇÇ : type_in_cat_univ A}
              (p : type_in_cat_univ_code a‚ÇÅ = type_in_cat_univ_code a‚ÇÇ)
              (q : cat_el_map_el_eq el p ¬∑ type_in_cat_univ_z_iso a‚ÇÇ
                   =
                   type_in_cat_univ_z_iso a‚ÇÅ)
    : a‚ÇÅ = a‚ÇÇ.
  Proof.
    induction a‚ÇÅ as [ a‚ÇÅ f‚ÇÅ ].
    induction a‚ÇÇ as [ a‚ÇÇ f‚ÇÇ ].
    cbn in *.
    induction p.
    apply maponpaths.
    use z_iso_eq.
    refine (!q @ _) ; cbn.
    apply id_left.
  Qed.

  (** * 2. Examples for codes *)

  (** ** 2.1. The terminal object *)
  Definition terminal_in_cat_univ
    : UU
    := type_in_cat_univ ùüô.

  Proposition terminal_in_cat_univ_eq
              {c‚ÇÅ c‚ÇÇ : terminal_in_cat_univ}
              (p : type_in_cat_univ_code c‚ÇÅ = type_in_cat_univ_code c‚ÇÇ)
    : c‚ÇÅ = c‚ÇÇ.
  Proof.
    use type_in_cat_univ_eq.
    - exact p.
    - apply TerminalArrowEq.
  Qed.

  (** ** 2.2. The initial object *)
  Definition initial_in_cat_univ
             (I : Initial C)
    : UU
    := type_in_cat_univ I.

  Proposition initial_in_cat_univ_eq
              {I : Initial C}
              {c‚ÇÅ c‚ÇÇ : initial_in_cat_univ I}
              (p : type_in_cat_univ_code c‚ÇÅ = type_in_cat_univ_code c‚ÇÇ)
    : c‚ÇÅ = c‚ÇÇ.
  Proof.
    use type_in_cat_univ_eq.
    - exact p.
    - refine (maponpaths
                pr1
                (z_iso_eq_inv
                   (z_iso_comp (cat_el_map_el_eq el p) (type_in_cat_univ_z_iso c‚ÇÇ))
                   (type_in_cat_univ_z_iso c‚ÇÅ)
                   _)).
      apply InitialArrowEq.
  Qed.

  (** ** 2.3. Parameterized natural numbers object *)
  Definition pnno_in_cat_univ
             (N : parameterized_NNO ùüô (binproducts_univ_cat_with_finlim C))
    : UU
    := type_in_cat_univ N.

  (** ** 2.4. Subobject classifier*)
  Definition subobject_classifier_in_cat_univ
             (Œ© : subobject_classifier ùüô)
    : UU
    := type_in_cat_univ Œ©.
End TypesInCatUniv.

Arguments type_in_cat_univ_code {C A} a.
Arguments type_in_cat_univ_z_iso {C A} a.
Arguments terminal_in_cat_univ C : clear implicits.

Section FunctorPreservesTypes.
  Context {C‚ÇÅ C‚ÇÇ : univ_cat_with_finlim_universe}
          (F : functor_finlim_universe C‚ÇÅ C‚ÇÇ).

  Let el‚ÇÅ : cat_stable_el_map_coherent C‚ÇÅ := univ_cat_cat_stable_el_map C‚ÇÅ.
  Let el‚ÇÇ : cat_stable_el_map_coherent C‚ÇÇ := univ_cat_cat_stable_el_map C‚ÇÇ.

  Let Fel : functor_preserves_el
              (univ_cat_cat_stable_el_map C‚ÇÅ)
              (univ_cat_cat_stable_el_map C‚ÇÇ)
              F
    := functor_finlim_universe_preserves_el F.

  (** * 3. Preservation by functors *)
  Definition functor_preserves_type_in_cat_univ_code
             {A‚ÇÅ : C‚ÇÅ}
             {A‚ÇÇ : C‚ÇÇ}
             (FA : z_iso (F A‚ÇÅ) A‚ÇÇ)
             (a‚ÇÅ : type_in_cat_univ A‚ÇÅ)
             (a‚ÇÇ : type_in_cat_univ A‚ÇÇ)
    : UU
    := #F(type_in_cat_univ_code a‚ÇÅ) ¬∑ functor_on_universe F
       =
       TerminalArrow _ _ ¬∑ type_in_cat_univ_code a‚ÇÇ.

  Definition functor_preserves_type_in_cat_univ_code_z_iso
             {A‚ÇÅ : C‚ÇÅ}
             {A‚ÇÇ : C‚ÇÇ}
             {FA : z_iso (F A‚ÇÅ) A‚ÇÇ}
             {a‚ÇÅ : type_in_cat_univ A‚ÇÅ}
             {a‚ÇÇ : type_in_cat_univ A‚ÇÇ}
             (Fc : functor_preserves_type_in_cat_univ_code FA a‚ÇÅ a‚ÇÇ)
    : UU
    := #F(type_in_cat_univ_z_iso a‚ÇÅ) ¬∑ FA
       =
       functor_el_map_iso Fel (type_in_cat_univ_code a‚ÇÅ)
       ¬∑ (cat_el_map_el_eq el‚ÇÇ Fc)
       ¬∑ cat_el_map_pb_mor _ _ _
       ¬∑ type_in_cat_univ_z_iso a‚ÇÇ.

  Definition functor_preserves_type_in_cat_univ
             {A‚ÇÅ : C‚ÇÅ}
             {A‚ÇÇ : C‚ÇÇ}
             (FA : z_iso (F A‚ÇÅ) A‚ÇÇ)
             (a‚ÇÅ : type_in_cat_univ A‚ÇÅ)
             (a‚ÇÇ : type_in_cat_univ A‚ÇÇ)
    : UU
    := ‚àë (p : functor_preserves_type_in_cat_univ_code FA a‚ÇÅ a‚ÇÇ),
       functor_preserves_type_in_cat_univ_code_z_iso p.

  Proposition isaprop_functor_preserves_type_in_cat_univ
              {A‚ÇÅ : C‚ÇÅ}
              {A‚ÇÇ : C‚ÇÇ}
              (FA : z_iso (F A‚ÇÅ) A‚ÇÇ)
              (a‚ÇÅ : type_in_cat_univ A‚ÇÅ)
              (a‚ÇÇ : type_in_cat_univ A‚ÇÇ)
    : isaprop (functor_preserves_type_in_cat_univ FA a‚ÇÅ a‚ÇÇ).
  Proof.
    use isaproptotal2.
    - intro.
      apply homset_property.
    - intros.
      apply homset_property.
  Qed.

  Definition functor_preserves_type_in_cat_univ_z_iso_eq
             {A‚ÇÅ : C‚ÇÅ}
             {A‚ÇÇ : C‚ÇÇ}
             {FA FA' : z_iso (F A‚ÇÅ) A‚ÇÇ}
             (p : FA = FA')
             {a‚ÇÅ : type_in_cat_univ A‚ÇÅ}
             {a‚ÇÇ : type_in_cat_univ A‚ÇÇ}
             (HF : functor_preserves_type_in_cat_univ FA a‚ÇÅ a‚ÇÇ)
    : functor_preserves_type_in_cat_univ FA' a‚ÇÅ a‚ÇÇ.
  Proof.
    induction p.
    exact HF.
  Qed.

  Definition make_functor_preserves_type_in_cat_univ
             {A‚ÇÅ : C‚ÇÅ}
             {A‚ÇÇ : C‚ÇÇ}
             {FA : z_iso (F A‚ÇÅ) A‚ÇÇ}
             {a‚ÇÅ : type_in_cat_univ A‚ÇÅ}
             {a‚ÇÇ : type_in_cat_univ A‚ÇÇ}
             (p : functor_preserves_type_in_cat_univ_code FA a‚ÇÅ a‚ÇÇ)
             (q : functor_preserves_type_in_cat_univ_code_z_iso p)
    : functor_preserves_type_in_cat_univ FA a‚ÇÅ a‚ÇÇ
    := p ,, q.

  Proposition functor_preserves_type_in_cat_univ_code_eq
              {A‚ÇÅ : C‚ÇÅ}
              {A‚ÇÇ : C‚ÇÇ}
              {FA : z_iso (F A‚ÇÅ) A‚ÇÇ}
              {a‚ÇÅ : type_in_cat_univ A‚ÇÅ}
              {a‚ÇÇ : type_in_cat_univ A‚ÇÇ}
              (Fc : functor_preserves_type_in_cat_univ FA a‚ÇÅ a‚ÇÇ)
    : #F(type_in_cat_univ_code a‚ÇÅ) ¬∑ functor_on_universe F
      =
      TerminalArrow _ _ ¬∑ type_in_cat_univ_code a‚ÇÇ.
  Proof.
    exact (pr1 Fc).
  Defined.

  Proposition functor_preserves_type_in_cat_univ_code_z_iso_eq
              {A‚ÇÅ : C‚ÇÅ}
              {A‚ÇÇ : C‚ÇÇ}
              {FA : z_iso (F A‚ÇÅ) A‚ÇÇ}
              {a‚ÇÅ : type_in_cat_univ A‚ÇÅ}
              {a‚ÇÇ : type_in_cat_univ A‚ÇÇ}
              (Fc : functor_preserves_type_in_cat_univ FA a‚ÇÅ a‚ÇÇ)
    : #F(type_in_cat_univ_z_iso a‚ÇÅ) ¬∑ FA
      =
      functor_el_map_iso Fel (type_in_cat_univ_code a‚ÇÅ)
      ¬∑ (cat_el_map_el_eq el‚ÇÇ (functor_preserves_type_in_cat_univ_code_eq Fc))
      ¬∑ cat_el_map_pb_mor _ _ _
      ¬∑ type_in_cat_univ_z_iso a‚ÇÇ.
  Proof.
    exact (pr2 Fc).
  Defined.
End FunctorPreservesTypes.

Arguments functor_preserves_type_in_cat_univ_code {C‚ÇÅ C‚ÇÇ} F {A‚ÇÅ A‚ÇÇ} FA a‚ÇÅ a‚ÇÇ /.
Arguments functor_preserves_type_in_cat_univ_code_z_iso /.

(** * 4. Examples for preservation *)

(** ** 4.1. Preservation of the code for the terminal object *)
Definition functor_preserves_terminal_in_cat_univ
           {C‚ÇÅ C‚ÇÇ : univ_cat_with_finlim_universe}
           (c‚ÇÅ : terminal_in_cat_univ C‚ÇÅ)
           (c‚ÇÇ : terminal_in_cat_univ C‚ÇÇ)
           (F : functor_finlim_universe C‚ÇÅ C‚ÇÇ)
  : UU
  := functor_preserves_type_in_cat_univ
       F
       (preserves_terminal_to_z_iso
          F (functor_finlim_preserves_terminal F)
          _ _)
       c‚ÇÅ
       c‚ÇÇ.

Proposition make_functor_preserves_terminal_in_cat_univ
            {C‚ÇÅ C‚ÇÇ : univ_cat_with_finlim_universe}
            {c‚ÇÅ : terminal_in_cat_univ C‚ÇÅ}
            {c‚ÇÇ : terminal_in_cat_univ C‚ÇÇ}
            {F : functor_finlim_universe C‚ÇÅ C‚ÇÇ}
            (p : #F(type_in_cat_univ_code c‚ÇÅ) ¬∑ functor_on_universe F
                 =
                 TerminalArrow _ _ ¬∑ type_in_cat_univ_code c‚ÇÇ)
  : functor_preserves_terminal_in_cat_univ c‚ÇÅ c‚ÇÇ F.
Proof.
  use make_functor_preserves_type_in_cat_univ.
  - exact p.
  - apply TerminalArrowEq.
Qed.

(** ** 4.2. Preservation of the code for the initial object *)
Definition functor_preserves_initial_in_cat_univ
           {C‚ÇÅ C‚ÇÇ : univ_cat_with_finlim_universe}
           {I‚ÇÅ : Initial C‚ÇÅ}
           {I‚ÇÇ : Initial C‚ÇÇ}
           (c‚ÇÅ : initial_in_cat_univ I‚ÇÅ)
           (c‚ÇÇ : initial_in_cat_univ I‚ÇÇ)
           (F : functor_finlim_universe C‚ÇÅ C‚ÇÇ)
           (FI : preserves_initial F)
  : UU
  := functor_preserves_type_in_cat_univ
       F
       (preserves_initial_to_z_iso
          F FI
          _ _)
       c‚ÇÅ
       c‚ÇÇ.

Proposition make_functor_preserves_initial_in_cat_univ
            {C‚ÇÅ C‚ÇÇ : univ_cat_with_finlim_universe}
            {I‚ÇÅ : Initial C‚ÇÅ}
            {I‚ÇÇ : Initial C‚ÇÇ}
            {c‚ÇÅ : initial_in_cat_univ I‚ÇÅ}
            {c‚ÇÇ : initial_in_cat_univ I‚ÇÇ}
            {F : functor_finlim_universe C‚ÇÅ C‚ÇÇ}
            {FI : preserves_initial F}
            (p : #F(type_in_cat_univ_code c‚ÇÅ) ¬∑ functor_on_universe F
                 =
                 TerminalArrow _ _ ¬∑ type_in_cat_univ_code c‚ÇÇ)
  : functor_preserves_initial_in_cat_univ c‚ÇÅ c‚ÇÇ F FI.
Proof.
  use make_functor_preserves_type_in_cat_univ.
  - exact p.
  - simpl.
    use (cancel_z_iso' (z_iso_inv (functor_on_z_iso F (type_in_cat_univ_z_iso c‚ÇÅ)))).
    apply (InitialArrowEq (O := preserves_initial_to_initial _ FI I‚ÇÅ)).
Qed.

Proposition functor_preserves_initial_in_cat_univ_eq
            {C‚ÇÅ C‚ÇÇ : univ_cat_with_finlim_universe}
            {I‚ÇÅ : Initial C‚ÇÅ}
            {I‚ÇÇ : Initial C‚ÇÇ}
            (c‚ÇÅ : initial_in_cat_univ I‚ÇÅ)
            (c‚ÇÇ : initial_in_cat_univ I‚ÇÇ)
            {F F' : functor_finlim_universe C‚ÇÅ C‚ÇÇ}
            (p : F = F')
            (FI : preserves_initial F)
            (FI' : preserves_initial F')
            (HF : functor_preserves_initial_in_cat_univ c‚ÇÅ c‚ÇÇ F FI)
  : functor_preserves_initial_in_cat_univ c‚ÇÅ c‚ÇÇ F' FI'.
Proof.
  induction p.
  assert (FI = FI') as ->.
  {
    apply isaprop_preserves_initial.
  }
  exact HF.
Qed.

(** ** 4.3. Preservation of the code for the parameterized natural numbers object *)
Definition functor_preserves_pnno_in_cat_univ
           {C‚ÇÅ C‚ÇÇ : univ_cat_with_finlim_universe}
           {N‚ÇÅ : parameterized_NNO
                   (terminal_univ_cat_with_finlim C‚ÇÅ)
                   (binproducts_univ_cat_with_finlim C‚ÇÅ)}
           {N‚ÇÇ : parameterized_NNO
                   (terminal_univ_cat_with_finlim C‚ÇÇ)
                   (binproducts_univ_cat_with_finlim C‚ÇÇ)}
           (c‚ÇÅ : pnno_in_cat_univ N‚ÇÅ)
           (c‚ÇÇ : pnno_in_cat_univ N‚ÇÇ)
           (F : functor_finlim_universe C‚ÇÅ C‚ÇÇ)
           (HF : preserves_parameterized_NNO
                   N‚ÇÅ N‚ÇÇ
                   F
                   (functor_finlim_preserves_terminal F))
  : UU
  := functor_preserves_type_in_cat_univ
       F
       (preserves_parameterized_NNO_z_iso HF)
       c‚ÇÅ
       c‚ÇÇ.

Proposition make_functor_preserves_pnno_in_cat_univ
            {C‚ÇÅ C‚ÇÇ : univ_cat_with_finlim_universe}
            {N‚ÇÅ : parameterized_NNO
                    (terminal_univ_cat_with_finlim C‚ÇÅ)
                    (binproducts_univ_cat_with_finlim C‚ÇÅ)}
            {N‚ÇÇ : parameterized_NNO
                    (terminal_univ_cat_with_finlim C‚ÇÇ)
                    (binproducts_univ_cat_with_finlim C‚ÇÇ)}
            {c‚ÇÅ : pnno_in_cat_univ N‚ÇÅ}
            {c‚ÇÇ : pnno_in_cat_univ N‚ÇÇ}
            {F : functor_finlim_universe C‚ÇÅ C‚ÇÇ}
            {HF : preserves_parameterized_NNO
                    N‚ÇÅ N‚ÇÇ
                    F
                    (functor_finlim_preserves_terminal F)}
            (p : #F(type_in_cat_univ_code c‚ÇÅ) ¬∑ functor_on_universe F
                 =
                 TerminalArrow _ _ ¬∑ type_in_cat_univ_code c‚ÇÇ)
            (q : #F (type_in_cat_univ_z_iso c‚ÇÅ)
                 =
                 functor_el_map_iso
                   (functor_finlim_universe_preserves_el F)
                   (type_in_cat_univ_code c‚ÇÅ)
                 ¬∑ cat_el_map_el_eq (univ_cat_cat_stable_el_map C‚ÇÇ) p
                 ¬∑ cat_el_map_pb_mor
                     (univ_cat_cat_stable_el_map C‚ÇÇ)
                     (TerminalArrow _ _)
                     (type_in_cat_univ_code c‚ÇÇ)
                 ¬∑ type_in_cat_univ_z_iso c‚ÇÇ
                 ¬∑ preserves_parameterized_NNO_mor
                     N‚ÇÅ N‚ÇÇ
                     F
                     (functor_finlim_preserves_terminal F))
  : functor_preserves_pnno_in_cat_univ c‚ÇÅ c‚ÇÇ F HF.
Proof.
  use make_functor_preserves_type_in_cat_univ.
  - exact p.
  - simpl.
    refine (!_).
    use z_iso_inv_on_left.
    exact q.
Qed.

Proposition functor_preserves_pnno_in_cat_univ_eq
            {C‚ÇÅ C‚ÇÇ : univ_cat_with_finlim_universe}
            {N‚ÇÅ : parameterized_NNO
                    (terminal_univ_cat_with_finlim C‚ÇÅ)
                    (binproducts_univ_cat_with_finlim C‚ÇÅ)}
            {N‚ÇÇ : parameterized_NNO
                    (terminal_univ_cat_with_finlim C‚ÇÇ)
                    (binproducts_univ_cat_with_finlim C‚ÇÇ)}
            {c‚ÇÅ : pnno_in_cat_univ N‚ÇÅ}
            {c‚ÇÇ : pnno_in_cat_univ N‚ÇÇ}
            {F F' : functor_finlim_universe C‚ÇÅ C‚ÇÇ}
            (p : F = F')
            (HF : preserves_parameterized_NNO
                    N‚ÇÅ N‚ÇÇ
                    F
                    (functor_finlim_preserves_terminal F))
            (HF' : preserves_parameterized_NNO
                     N‚ÇÅ N‚ÇÇ
                     F'
                     (functor_finlim_preserves_terminal F'))
            (HFF : functor_preserves_pnno_in_cat_univ c‚ÇÅ c‚ÇÇ F HF)
  : functor_preserves_pnno_in_cat_univ c‚ÇÅ c‚ÇÇ F' HF'.
Proof.
  induction p.
  assert (HF = HF') as ->.
  {
    apply isaprop_preserves_parameterized_NNO.
  }
  exact HFF.
Qed.

(** ** 4.4. Preservation of the code for the subobject classifier *)
Definition functor_preserves_subobject_classifier_in_cat_univ
           {C‚ÇÅ C‚ÇÇ : univ_cat_with_finlim_universe}
           {Œ©‚ÇÅ : subobject_classifier (terminal_univ_cat_with_finlim C‚ÇÅ)}
           {Œ©‚ÇÇ : subobject_classifier (terminal_univ_cat_with_finlim C‚ÇÇ)}
           (c‚ÇÅ : subobject_classifier_in_cat_univ Œ©‚ÇÅ)
           (c‚ÇÇ : subobject_classifier_in_cat_univ Œ©‚ÇÇ)
           (F : functor_finlim_universe C‚ÇÅ C‚ÇÇ)
           (HF : preserves_subobject_classifier
                   F
                   (terminal_univ_cat_with_finlim C‚ÇÅ)
                   (terminal_univ_cat_with_finlim C‚ÇÇ)
                   (functor_finlim_preserves_terminal F))
  : UU
  := functor_preserves_type_in_cat_univ
       F
       (preserves_subobject_classifier_z_iso HF Œ©‚ÇÅ Œ©‚ÇÇ)
       c‚ÇÅ
       c‚ÇÇ.

Proposition make_functor_preserves_subobject_classifier_in_cat_univ
            {C‚ÇÅ C‚ÇÇ : univ_cat_with_finlim_universe}
            {Œ©‚ÇÅ : subobject_classifier (terminal_univ_cat_with_finlim C‚ÇÅ)}
            {Œ©‚ÇÇ : subobject_classifier (terminal_univ_cat_with_finlim C‚ÇÇ)}
            {c‚ÇÅ : subobject_classifier_in_cat_univ Œ©‚ÇÅ}
            {c‚ÇÇ : subobject_classifier_in_cat_univ Œ©‚ÇÇ}
            {F : functor_finlim_universe C‚ÇÅ C‚ÇÇ}
            {HF : preserves_subobject_classifier
                    F
                    (terminal_univ_cat_with_finlim C‚ÇÅ)
                    (terminal_univ_cat_with_finlim C‚ÇÇ)
                    (functor_finlim_preserves_terminal F)}
            (p : #F(type_in_cat_univ_code c‚ÇÅ) ¬∑ functor_on_universe F
                 =
                 TerminalArrow _ _ ¬∑ type_in_cat_univ_code c‚ÇÇ)
            (q : functor_el_map_iso
                   (functor_finlim_universe_preserves_el F)
                   (type_in_cat_univ_code c‚ÇÅ)
                 ¬∑ cat_el_map_el_eq (univ_cat_cat_stable_el_map C‚ÇÇ) p
                 ¬∑ cat_el_map_pb_mor
                     (univ_cat_cat_stable_el_map C‚ÇÇ)
                     (TerminalArrow _ _)
                     (type_in_cat_univ_code c‚ÇÇ)
                 ¬∑ type_in_cat_univ_z_iso c‚ÇÇ
                 =
                 #F (type_in_cat_univ_z_iso c‚ÇÅ)
                 ¬∑ mor_subobject_classifier (preserves_subobject_classifier_on_ob HF Œ©‚ÇÅ) Œ©‚ÇÇ)
  : functor_preserves_subobject_classifier_in_cat_univ c‚ÇÅ c‚ÇÇ F HF.
Proof.
  use make_functor_preserves_type_in_cat_univ.
  - exact p.
  - simpl.
    refine (!_).
    exact q.
Qed.

Proposition functor_preserves_subobject_classifier_in_cat_univ_eq
            {C‚ÇÅ C‚ÇÇ : univ_cat_with_finlim_universe}
            {Œ©‚ÇÅ : subobject_classifier (terminal_univ_cat_with_finlim C‚ÇÅ)}
            {Œ©‚ÇÇ : subobject_classifier (terminal_univ_cat_with_finlim C‚ÇÇ)}
            {c‚ÇÅ : subobject_classifier_in_cat_univ Œ©‚ÇÅ}
            {c‚ÇÇ : subobject_classifier_in_cat_univ Œ©‚ÇÇ}
            {F F' : functor_finlim_universe C‚ÇÅ C‚ÇÇ}
            (p : F = F')
            (HF : preserves_subobject_classifier
                    F
                    (terminal_univ_cat_with_finlim C‚ÇÅ)
                    (terminal_univ_cat_with_finlim C‚ÇÇ)
                    (functor_finlim_preserves_terminal F))
            (HF' : preserves_subobject_classifier
                     F'
                     (terminal_univ_cat_with_finlim C‚ÇÅ)
                     (terminal_univ_cat_with_finlim C‚ÇÇ)
                     (functor_finlim_preserves_terminal F'))
            (HFF : functor_preserves_subobject_classifier_in_cat_univ c‚ÇÅ c‚ÇÇ F HF)
  : functor_preserves_subobject_classifier_in_cat_univ c‚ÇÅ c‚ÇÇ F' HF'.
Proof.
  induction p.
  assert (HF = HF') as ->.
  {
    apply isaprop_preserves_subobject_classifier.
  }
  exact HFF.
Qed.

(** * 5. Preservation by identity and composition *)
Proposition identity_preserves_type_in_cat_univ_code_eq
            {C : univ_cat_with_finlim_universe}
            {A : C}
            (a : type_in_cat_univ A)
  : functor_preserves_type_in_cat_univ_code
      (identity _)
      (identity_z_iso A)
      a
      a.
Proof.
  cbn.
  rewrite id_right.
  refine (!(id_left _) @ _).
  apply maponpaths_2.
  apply TerminalArrowEq.
Qed.

Proposition identity_preserves_type_in_cat_univ_z_iso_eq
            {C : univ_cat_with_finlim_universe}
            {A : C}
            (a : type_in_cat_univ A)
  : functor_preserves_type_in_cat_univ_code_z_iso
      (identity _)
      (identity_preserves_type_in_cat_univ_code_eq a).
Proof.
  cbn.
  refine (id_right _ @ _).
  refine (!(id_left _) @ _).
  apply maponpaths_2.
  refine (!_).
  etrans.
  {
    apply maponpaths.
    use cat_el_map_pb_mor_id'.
    apply TerminalArrowEq.
  }
  rewrite !(cat_el_map_el_eq_comp (univ_cat_cat_stable_el_map C)).
  apply (cat_el_map_el_eq_id (univ_cat_cat_stable_el_map C)).
Qed.

Proposition identity_preserves_type_in_cat_univ
            {C : univ_cat_with_finlim_universe}
            {A : C}
            (a : type_in_cat_univ A)
  : functor_preserves_type_in_cat_univ
      (identity _)
      (identity_z_iso A)
      a
      a.
Proof.
  use make_functor_preserves_type_in_cat_univ.
  - exact (identity_preserves_type_in_cat_univ_code_eq a).
  - exact (identity_preserves_type_in_cat_univ_z_iso_eq a).
Qed.

Section CompPreservation.
  Context {C‚ÇÅ C‚ÇÇ C‚ÇÉ : univ_cat_with_finlim_universe}
          (F : functor_finlim_universe C‚ÇÅ C‚ÇÇ)
          (G : functor_finlim_universe C‚ÇÇ C‚ÇÉ)
          {A‚ÇÅ : C‚ÇÅ}
          {A‚ÇÇ : C‚ÇÇ}
          {A‚ÇÉ : C‚ÇÉ}
          {a‚ÇÅ : type_in_cat_univ A‚ÇÅ}
          {a‚ÇÇ : type_in_cat_univ A‚ÇÇ}
          {a‚ÇÉ : type_in_cat_univ A‚ÇÉ}
          {FA : z_iso (F A‚ÇÅ) A‚ÇÇ}
          {GA : z_iso (G A‚ÇÇ) A‚ÇÉ}
          (FcA : functor_preserves_type_in_cat_univ F FA a‚ÇÅ a‚ÇÇ)
          (GcA : functor_preserves_type_in_cat_univ G GA a‚ÇÇ a‚ÇÉ).

  Proposition comp_functor_preserves_type_in_cat_univ_code_eq
    : functor_preserves_type_in_cat_univ_code
        (F ¬∑ G)
        (z_iso_comp (functor_on_z_iso G FA) GA)
        a‚ÇÅ
        a‚ÇÉ.
  Proof.
    cbn.
    rewrite !assoc.
    rewrite <- functor_comp.
    etrans.
    {
      apply maponpaths_2.
      apply maponpaths.
      apply (functor_preserves_type_in_cat_univ_code_eq _ FcA).
    }
    rewrite functor_comp.
    rewrite !assoc'.
    etrans.
    {
      apply maponpaths.
      apply (functor_preserves_type_in_cat_univ_code_eq _ GcA).
    }
    rewrite !assoc.
    apply maponpaths_2.
    apply TerminalArrowEq.
  Qed.

  Proposition comp_functor_preserves_type_in_cat_univ_z_iso_eq
    : functor_preserves_type_in_cat_univ_code_z_iso
        (F ¬∑ G)
        comp_functor_preserves_type_in_cat_univ_code_eq.
  Proof.
    cbn.
    refine (assoc _ _ _ @ _).
    rewrite <- functor_comp.
    etrans.
    {
      apply maponpaths_2.
      apply maponpaths.
      apply FcA.
    }
    rewrite functor_comp.
    refine (assoc' _ _ _ @ _).
    etrans.
    {
      apply maponpaths.
      apply GcA.
    }
    refine (assoc _ _ _ @ _).
    apply maponpaths_2.
    rewrite !functor_comp.
    do 2 refine (assoc' _ _ _ @ _).
    do 2 refine (_ @ assoc _ _ _).
    apply maponpaths.
    etrans.
    {
      apply maponpaths.
      rewrite !assoc.
      do 2 apply maponpaths_2.
      apply (functor_preserves_el_path (functor_finlim_universe_preserves_el G)).
    }
    refine (assoc _ _ _ @ _).
    etrans.
    {
      apply maponpaths_2.
      refine (assoc _ _ _ @ _).
      apply maponpaths_2.
      refine (assoc _ _ _ @ _).
      apply maponpaths_2.
      refine (assoc _ _ _ @ _).
      apply maponpaths_2.
      apply (functor_el_map_iso_eq_alt (functor_finlim_universe_preserves_el G)).
    }
    do 4 refine (assoc' _ _ _ @ _).
    refine (_ @ assoc _ _ _).
    apply maponpaths.
    etrans.
    {
      do 2 apply maponpaths.
      refine (assoc _ _ _ @ _).
      apply maponpaths_2.
      refine (!_).
      use cat_el_map_pb_mor_eq.
      etrans.
      {
        apply maponpaths.
        apply GcA.
      }
      rewrite !assoc.
      apply idpath.
    }
    etrans.
    {
      do 2 apply maponpaths.
      refine (assoc' _ _ _ @ _).
      apply maponpaths.
      use cat_el_map_pb_mor_comp'.
    }
    do 3 refine (assoc _ _ _ @ _).
    refine (_ @ assoc' _ _ _).
    etrans.
    {
      do 3 apply maponpaths_2.
      apply (cat_el_map_el_eq_comp (univ_cat_cat_stable_el_map C‚ÇÉ)).
    }
    etrans.
    {
      do 2 apply maponpaths_2.
      apply (cat_el_map_el_eq_comp (univ_cat_cat_stable_el_map C‚ÇÉ)).
    }
    etrans.
    {
      apply maponpaths_2.
      apply (cat_el_map_el_eq_comp (univ_cat_cat_stable_el_map C‚ÇÉ)).
    }
    etrans.
    {
      apply maponpaths.
      use cat_el_map_pb_mor_subst_eq.
      - apply TerminalArrow.
      - apply TerminalArrowEq.
    }
    refine (assoc _ _ _ @ _).
    apply maponpaths_2.
    etrans.
    {
      apply (cat_el_map_el_eq_comp (univ_cat_cat_stable_el_map C‚ÇÉ)).
    }
    refine (!_).
    simpl.
    etrans.
    {
      apply (cat_el_map_el_eq_comp (univ_cat_cat_stable_el_map C‚ÇÉ)).
    }
    apply (cat_el_map_el_eq_eq (univ_cat_cat_stable_el_map C‚ÇÉ)).
  Qed.

  Proposition comp_functor_preserves_type_in_cat_univ
    : functor_preserves_type_in_cat_univ
        (F ¬∑ G)
        (z_iso_comp (functor_on_z_iso G FA) GA)
        a‚ÇÅ
        a‚ÇÉ.
  Proof.
    use make_functor_preserves_type_in_cat_univ.
    - exact comp_functor_preserves_type_in_cat_univ_code_eq.
    - exact comp_functor_preserves_type_in_cat_univ_z_iso_eq.
  Qed.
End CompPreservation.

(** * 6. Instantiation to the examples *)

(** ** 6.1. Preservation of the code for the terminal object *)
Proposition id_functor_preserves_terminal_in_cat_univ
            {C : univ_cat_with_finlim_universe}
            (c : terminal_in_cat_univ C)
  : functor_preserves_terminal_in_cat_univ
      c
      c
      (identity _).
Proof.
  refine (functor_preserves_type_in_cat_univ_z_iso_eq
            _
            _
            (identity_preserves_type_in_cat_univ c)).
  use z_iso_eq.
  apply TerminalArrowEq.
Qed.

Proposition comp_functor_preserves_terminal_in_cat_univ
            {C‚ÇÅ C‚ÇÇ C‚ÇÉ : univ_cat_with_finlim_universe}
            {F : functor_finlim_universe C‚ÇÅ C‚ÇÇ}
            {G : functor_finlim_universe C‚ÇÇ C‚ÇÉ}
            {c‚ÇÅ : terminal_in_cat_univ C‚ÇÅ}
            {c‚ÇÇ : terminal_in_cat_univ C‚ÇÇ}
            {c‚ÇÉ : terminal_in_cat_univ C‚ÇÉ}
            (Fc : functor_preserves_terminal_in_cat_univ c‚ÇÅ c‚ÇÇ F)
            (Gc : functor_preserves_terminal_in_cat_univ c‚ÇÇ c‚ÇÉ G)
  : functor_preserves_terminal_in_cat_univ
      c‚ÇÅ
      c‚ÇÉ
      (F ¬∑ G).
Proof.
  refine (functor_preserves_type_in_cat_univ_z_iso_eq
            _
            _
            (comp_functor_preserves_type_in_cat_univ F G Fc Gc)).
  use z_iso_eq.
  apply TerminalArrowEq.
Qed.

(** ** 6.2. Preservation of the code for the initial object *)
Proposition id_functor_preserves_initial_in_cat_univ
            {C : univ_cat_with_finlim_universe}
            {I : Initial C}
            (c : initial_in_cat_univ I)
  : functor_preserves_initial_in_cat_univ
      c
      c
      (identity _)
      (identity_preserves_initial _).
Proof.
  refine (functor_preserves_type_in_cat_univ_z_iso_eq
            _
            _
            (identity_preserves_type_in_cat_univ c)).
  use z_iso_eq.
  apply InitialArrowEq.
Qed.

Proposition comp_functor_preserves_initial_in_cat_univ
            {C‚ÇÅ C‚ÇÇ C‚ÇÉ : univ_cat_with_finlim_universe}
            {F : functor_finlim_universe C‚ÇÅ C‚ÇÇ}
            {HF : preserves_initial F}
            {G : functor_finlim_universe C‚ÇÇ C‚ÇÉ}
            {HG : preserves_initial G}
            {I‚ÇÅ : Initial C‚ÇÅ}
            {I‚ÇÇ : Initial C‚ÇÇ}
            {I‚ÇÉ : Initial C‚ÇÉ}
            {c‚ÇÅ : initial_in_cat_univ I‚ÇÅ}
            {c‚ÇÇ : initial_in_cat_univ I‚ÇÇ}
            {c‚ÇÉ : initial_in_cat_univ I‚ÇÉ}
            (Fc : functor_preserves_initial_in_cat_univ c‚ÇÅ c‚ÇÇ F HF)
            (Gc : functor_preserves_initial_in_cat_univ c‚ÇÇ c‚ÇÉ G HG)
  : functor_preserves_initial_in_cat_univ
      c‚ÇÅ
      c‚ÇÉ
      (F ¬∑ G)
      (composition_preserves_initial HF HG).
Proof.
  refine (functor_preserves_type_in_cat_univ_z_iso_eq
            _
            _
            (comp_functor_preserves_type_in_cat_univ F G Fc Gc)).
  use z_iso_eq.
  apply (InitialArrowEq
           (O := preserves_initial_to_initial _ (composition_preserves_initial HF HG) I‚ÇÅ)).
Qed.

(** ** 6.3. Preservation of the code for the parameterized natural numbers object *)
Proposition id_functor_preserves_pnno_in_cat_univ
            {C : univ_cat_with_finlim_universe}
            {N : parameterized_NNO
                   (terminal_univ_cat_with_finlim C)
                   (binproducts_univ_cat_with_finlim C)}
            (c : pnno_in_cat_univ N)
  : functor_preserves_pnno_in_cat_univ
      c
      c
      (identity _)
      (id_preserves_parameterized_NNO _ _ _).
Proof.
  exact (identity_preserves_type_in_cat_univ c).
Qed.

Proposition comp_functor_preserves_pnno_in_cat_univ
            {C‚ÇÅ C‚ÇÇ C‚ÇÉ : univ_cat_with_finlim_universe}
            {F : functor_finlim_universe C‚ÇÅ C‚ÇÇ}
            {G : functor_finlim_universe C‚ÇÇ C‚ÇÉ}
            {N‚ÇÅ : parameterized_NNO
                    (terminal_univ_cat_with_finlim C‚ÇÅ)
                    (binproducts_univ_cat_with_finlim C‚ÇÅ)}
            {N‚ÇÇ : parameterized_NNO
                    (terminal_univ_cat_with_finlim C‚ÇÇ)
                    (binproducts_univ_cat_with_finlim C‚ÇÇ)}
            {N‚ÇÉ : parameterized_NNO
                    (terminal_univ_cat_with_finlim C‚ÇÉ)
                    (binproducts_univ_cat_with_finlim C‚ÇÉ)}
            {c‚ÇÅ : pnno_in_cat_univ N‚ÇÅ}
            {c‚ÇÇ : pnno_in_cat_univ N‚ÇÇ}
            {c‚ÇÉ : pnno_in_cat_univ N‚ÇÉ}
            {HF : preserves_parameterized_NNO
                    N‚ÇÅ N‚ÇÇ
                    F
                    (functor_finlim_preserves_terminal F)}
            {HG : preserves_parameterized_NNO
                    N‚ÇÇ N‚ÇÉ
                    G
                    (functor_finlim_preserves_terminal G)}
            (Fc : functor_preserves_pnno_in_cat_univ c‚ÇÅ c‚ÇÇ F HF)
            (Gc : functor_preserves_pnno_in_cat_univ c‚ÇÇ c‚ÇÉ G HG)
  : functor_preserves_pnno_in_cat_univ
      c‚ÇÅ
      c‚ÇÉ
      (F ¬∑ G)
      (comp_preserves_parameterized_NNO HF HG).
Proof.
  exact (comp_functor_preserves_type_in_cat_univ F G Fc Gc).
Qed.

(** ** 6.4. Preservation of the code for the subobject classifier *)
Proposition id_functor_preserves_subobject_classifier_in_cat_univ
            {C : univ_cat_with_finlim_universe}
            {Œ© : subobject_classifier (terminal_univ_cat_with_finlim C)}
            (c : subobject_classifier_in_cat_univ Œ©)
  : functor_preserves_subobject_classifier_in_cat_univ
      c
      c
      (identity _)
      (identity_preserves_subobject_classifier _).
Proof.
  refine (functor_preserves_type_in_cat_univ_z_iso_eq
            _
            _
            (identity_preserves_type_in_cat_univ c)).
  use z_iso_eq ; cbn.
  apply mor_subobject_classifier_is_identity.
Qed.

Proposition comp_functor_preserves_subobject_classifier_in_cat_univ
            {C‚ÇÅ C‚ÇÇ C‚ÇÉ : univ_cat_with_finlim_universe}
            {F : functor_finlim_universe C‚ÇÅ C‚ÇÇ}
            {G : functor_finlim_universe C‚ÇÇ C‚ÇÉ}
            {Œ©‚ÇÅ : subobject_classifier (terminal_univ_cat_with_finlim C‚ÇÅ)}
            {Œ©‚ÇÇ : subobject_classifier (terminal_univ_cat_with_finlim C‚ÇÇ)}
            {Œ©‚ÇÉ : subobject_classifier (terminal_univ_cat_with_finlim C‚ÇÉ)}
            {c‚ÇÅ : subobject_classifier_in_cat_univ Œ©‚ÇÅ}
            {c‚ÇÇ : subobject_classifier_in_cat_univ Œ©‚ÇÇ}
            {c‚ÇÉ : subobject_classifier_in_cat_univ Œ©‚ÇÉ}
            {HF : preserves_subobject_classifier
                    F
                    (terminal_univ_cat_with_finlim C‚ÇÅ)
                    (terminal_univ_cat_with_finlim C‚ÇÇ)
                    (functor_finlim_preserves_terminal F)}
            {HG : preserves_subobject_classifier
                    G
                    (terminal_univ_cat_with_finlim C‚ÇÇ)
                    (terminal_univ_cat_with_finlim C‚ÇÉ)
                    (functor_finlim_preserves_terminal G)}
            (Fc : functor_preserves_subobject_classifier_in_cat_univ c‚ÇÅ c‚ÇÇ F HF)
            (Gc : functor_preserves_subobject_classifier_in_cat_univ c‚ÇÇ c‚ÇÉ G HG)
  : functor_preserves_subobject_classifier_in_cat_univ
      c‚ÇÅ
      c‚ÇÉ
      (F ¬∑ G)
      (comp_preserves_subobject_classifier HF HG).
Proof.
  refine (functor_preserves_type_in_cat_univ_z_iso_eq
            _
            _
            (comp_functor_preserves_type_in_cat_univ F G Fc Gc)).
  use z_iso_eq ; cbn.
  apply mor_subobject_classifier_is_composition.
Qed.

(** * 7. Univalence condition *)
Proposition type_in_cat_univ_univalence_lemma
            {C : univ_cat_with_finlim_universe}
            {A : C}
            {a‚ÇÅ a‚ÇÇ : type_in_cat_univ A}
            (Fc : functor_preserves_type_in_cat_univ
                     (identity _)
                     (identity_z_iso _)
                     a‚ÇÅ a‚ÇÇ)
  : a‚ÇÅ = a‚ÇÇ.
Proof.
  use type_in_cat_univ_eq.
  - pose (functor_preserves_type_in_cat_univ_code_eq _ Fc) as p.
    simpl in p.
    refine (!(id_right _) @ _).
    refine (p @ _).
    refine (_ @ id_left _).
    apply maponpaths_2.
    apply TerminalArrowEq.
  - pose (functor_preserves_type_in_cat_univ_code_z_iso_eq _ Fc) as p.
    simpl in p.
    refine (!_).
    refine (!(id_right _) @ _).
    refine (p @ _).
    apply maponpaths_2.
    etrans.
    {
      apply maponpaths.
      use cat_el_map_pb_mor_subst_eq.
      {
        apply identity.
      }
      apply TerminalArrowEq.
    }
    etrans.
    {
      do 2 apply maponpaths.
      apply cat_el_map_pb_mor_id.
    }
    rewrite !(cat_el_map_el_eq_comp (univ_cat_cat_stable_el_map C)).
    apply (cat_el_map_el_eq_eq (univ_cat_cat_stable_el_map C)).
Qed.

(** ** 7.1. Univalence condition for the terminal object *)
Proposition terminal_in_cat_univ_univalence_lemma
            {C : univ_cat_with_finlim_universe}
            {c‚ÇÅ c‚ÇÇ : terminal_in_cat_univ C}
            (Fc : functor_preserves_terminal_in_cat_univ c‚ÇÅ c‚ÇÇ (identity _))
  : c‚ÇÅ = c‚ÇÇ.
Proof.
  use type_in_cat_univ_univalence_lemma.
  refine (functor_preserves_type_in_cat_univ_z_iso_eq
            _
            _
            Fc).
  use z_iso_eq.
  apply TerminalArrowEq.
Qed.

(** ** 7.2. Univalence condition for the initial object *)
Proposition initial_in_cat_univ_univalence_lemma
            {C : univ_cat_with_finlim_universe}
            {I : Initial C}
            {c‚ÇÅ c‚ÇÇ : initial_in_cat_univ I}
            (Fc : functor_preserves_initial_in_cat_univ
                    c‚ÇÅ c‚ÇÇ
                    (identity _)
                    (identity_preserves_initial _))
  : c‚ÇÅ = c‚ÇÇ.
Proof.
  use type_in_cat_univ_univalence_lemma.
  refine (functor_preserves_type_in_cat_univ_z_iso_eq
            _
            _
            Fc).
  use z_iso_eq.
  apply InitialArrowEq.
Qed.

(** ** 7.3. Univalence condition for the parameterized natural numbers object *)
Proposition pnno_in_cat_univ_univalence_lemma
            {C : univ_cat_with_finlim_universe}
            {N : parameterized_NNO
                   (terminal_univ_cat_with_finlim C)
                   (binproducts_univ_cat_with_finlim C)}
            {c‚ÇÅ c‚ÇÇ : pnno_in_cat_univ N}
            (Fc : functor_preserves_pnno_in_cat_univ
                    c‚ÇÅ c‚ÇÇ
                    (identity _)
                    (id_preserves_parameterized_NNO _ _ _))
  : c‚ÇÅ = c‚ÇÇ.
Proof.
  use type_in_cat_univ_univalence_lemma.
  exact Fc.
Qed.

(** ** 7.4. Univalence condition for the subobject classifier *)
Proposition subobject_classifier_in_cat_univ_univalence_lemma
            {C : univ_cat_with_finlim_universe}
            {Œ© : subobject_classifier (terminal_univ_cat_with_finlim C)}
            {c‚ÇÅ c‚ÇÇ : subobject_classifier_in_cat_univ Œ©}
            (Fc : functor_preserves_subobject_classifier_in_cat_univ
                    c‚ÇÅ c‚ÇÇ
                    (identity _)
                    (identity_preserves_subobject_classifier _))
  : c‚ÇÅ = c‚ÇÇ.
Proof.
  use type_in_cat_univ_univalence_lemma.
  refine (functor_preserves_type_in_cat_univ_z_iso_eq
            _
            _
            Fc).
  use z_iso_eq ; cbn.
  refine (!_).
  apply mor_subobject_classifier_is_identity.
Qed.
